apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.oasisfeng.island"
        minSdkVersion 21
        targetSdkVersion 27     // Module "fileprovider" indirectly accessed private APIs in dark greylist, via loaded remote class.
        resConfigs "en", "zh"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    dataBinding.enabled true

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "packaging"

    productFlavors {
        complete {
            dimension "packaging"
        }
        engine {    // Use the same application ID to retain the profile / device ownership between "engine" and "full" build.
            dimension "packaging"
            matchingFallbacks = ['full']
        }
        mobile {
            applicationIdSuffix ".mobile"
            dimension "packaging"
            matchingFallbacks = ['full']
        }
        fileprovider {
            applicationIdSuffix ".fileprovider"
            dimension "packaging"
            matchingFallbacks = ['full']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        check 'NewApi'
        abortOnError true
        htmlReport false
        xmlReport false
        textReport true
        textOutput "stdout"
    }

    repositories.flatDir {
        dirs '../app/libs'
    }
}

dependencies {
    // Complete
    completeImplementation project(':engine')
    completeImplementation project(':mobile')
    completeImplementation project(':fileprovider')
    completeImplementation project(':installer')
    // Engine only
    engineImplementation project(':engine')
    // Mobile only
    mobileImplementation project(':mobile')
    // File Provider only
    fileproviderImplementation project(':fileprovider')
}

// Firebase plug-in
apply plugin: 'com.google.gms.google-services'
