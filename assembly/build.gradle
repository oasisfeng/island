apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion this.compileSdkVersion

    defaultConfig {
        applicationId "com.oasisfeng.island"
        minSdkVersion this.minSdkVersion
        targetSdkVersion 29     // Private APIs are accessed by module "open" and "fileprovider" (via indirectly loaded remote class)
        resConfigs "en", "zh"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildFeatures.dataBinding true

    buildTypes {
        debug {
            firebaseCrashlytics.mappingFileUploadEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "packaging"

    productFlavors {
        complete {
            dimension "packaging"
        }
        engine {    // Use the same application ID to retain the profile / device ownership between "engine" and "full" build.
            dimension "packaging"
            matchingFallbacks = ['full']
        }
        mobile {
            applicationIdSuffix ".mobile"
            dimension "packaging"
            matchingFallbacks = ['full']
        }
        fileprovider {
            applicationIdSuffix ".fileprovider"
            dimension "packaging"
            matchingFallbacks = ['full']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        check 'NewApi'
        abortOnError true
        htmlReport false
        xmlReport false
        textReport true
        textOutput "stdout"
    }

    repositories.flatDir {
        dirs '../app/libs'
    }
}

dependencies {
    // Complete
    completeImplementation project(':engine')
    completeImplementation project(':mobile')
    completeImplementation project(':fileprovider')
    completeImplementation project(':installer')
    completeImplementation project(':watcher')
    completeImplementation project(':open')
    // Engine only
    engineImplementation project(':engine')
    // Mobile only
    mobileImplementation project(':mobile')
    // File Provider only
    fileproviderImplementation project(':fileprovider')
}
